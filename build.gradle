apply plugin: 'groovy'

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

repositories {
    mavenCentral()
    maven { url 'http://dl.bintray.com/gesellix/docker-utils' }
}

dependencies {
    compile localGroovy()
    compile 'de.gesellix:docker-client:2016-04-11T22-50-38'
    compile 'org.slf4j:slf4j-api:1.7.5'
    compile 'org.slf4j:slf4j-simple:1.6.4'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

test {
    reports {
        html.enabled = true
    }
}

task runScript(type: JavaExec) {
    description 'Run Groovy script'

    main = 'cosmos.cosmonaut.Launcher'

    classpath = sourceSets.main.runtimeClasspath

    def scriptsDir = "$projectDir/scripts"
    def cosmosUrl = project.hasProperty('cosmosUrl') ? project.property('cosmosUrl') : ''
    def tableName = project.hasProperty('cosmosTable') ? project.property('cosmosTable') : ''
    def keyword = project.hasProperty('keyword') ? project.property('keyword') : ''

    args scriptsDir, cosmosUrl, tableName, keyword
}

defaultTasks 'runScript'
